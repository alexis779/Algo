plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group 'org.techvineyard'
version '1.0'

repositories {
    mavenCentral()
}

dependencies {
    api 'org.slf4j:slf4j-api:2.0.3'
    api 'org.slf4j:slf4j-log4j12:2.0.3'
    api 'org.apache.commons:commons-math3:3.6.1'
    api 'com.google.guava:guava:31.1-jre'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.1'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

test {
    useJUnitPlatform()
}

java {
    withSourcesJar()
    withJavadocJar()
}

publishing {
    repositories {
        maven {
            name = "OSSRH"
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_PASSWORD")
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            from components.java

            groupId = 'io.github.alexis779'
            artifactId = 'algo'

            pom {
                name = 'Tech Vineyard Algo library'
                description = 'A Java library for Algo & Data Structures'
                url = 'https://techvineyard.blogspot.com/'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'alexis779'
                        name = 'Alexis'
                        email = 'alexis@apache.org'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/alexis779/Algo.git'
                    developerConnection = 'scm:git:ssh://github.com/alexis779/Algo.git'
                    url = 'https://github.com/alexis779/Algo'
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}